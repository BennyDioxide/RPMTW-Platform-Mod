repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://zoidberg.ukp.informatik.tu-darmstadt.de/artifactory/public-releases/' }
    maven { url 'https://maven.blamejared.com' }
}
buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'siongsng.rpmtwupdatemod'
version = '1.2.1'
archivesBaseName = 'RPMTW-Update-Mod-1.12'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                rpmtwupdatemod {
                    source sourceSets.main
                }
            }
        }
    }
}
sourceSets.main.resources { srcDir 'src/generated/resources' }
configurations {
    shade
    compile.extendsFrom(shade)
}
dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'
    shade('commons-lang:commons-lang:2.6')
    shade('net.sf.ezmorph:ezmorph:1.0.6')
    shade('commons-collections:commons-collections:3.2.2')
    shade('commons-beanutils:commons-beanutils:1.9.4')
    shade('commons-io:commons-io:2.9.0')
    shade('commons-logging:commons-logging:1.2')
    shade('io.socket:socket.io-client:2.0.1')
    shade('io.socket:engine.io-client:2.0.0')
    shade('org.json:json:20090211')
    shade('com.squareup.okhttp3:okhttp:3.12.12')
    shade('com.squareup.okio:okio:1.15.0')
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
jar {
    manifest {
        attributes([
                "Specification-Title"     : "rpmtw_update_mod",
                "Specification-Vendor"    : "SiongSng",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "SiongSng",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
jar.finalizedBy('reobfJar')
shadowJar {
    configurations = [project.configurations.shade]
    classifier = ''
}
reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}
artifacts {
    archives tasks.shadowJar
}
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives deobfJar
}